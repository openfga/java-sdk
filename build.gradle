plugins {
    id 'java'

    // Quality
    id 'jacoco'
    id 'jvm-test-suite'
    id 'com.diffplug.spotless' version '6.25.0'

    // IDE
    id 'idea'
    id 'eclipse'

    // Publishing
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

apply from: 'publish.gradle'

group = 'dev.openfga'
version = '0.5.0'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
}

javadoc {
    // Ignore warnings.
    options.addStringOption('Xdoclint:none', '-quiet')
}

test {
    // JaCoCo coverage report is always generated after tests run.
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    // tests are required to run before generating a JaCoCo coverage report.
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = true
    }
}

ext {
    jackson_version = "2.17.1"
    junit_version = "5.10.3"
}

dependencies {
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    implementation "org.openapitools:jackson-databind-nullable:0.2.6"
    implementation platform("io.opentelemetry:opentelemetry-bom:1.40.0")
    implementation "io.opentelemetry:opentelemetry-api"
}

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation project()
                implementation "org.junit.jupiter:junit-jupiter:$junit_version"
                implementation "org.mockito:mockito-core:5.+"
                runtimeOnly "org.junit.platform:junit-platform-launcher"
                implementation "org.wiremock:wiremock:3.8.0"

                // This test-only dependency is convenient but not widely used.
                // Review project activity before updating the version here.
                // See also: https://github.com/PGSSoft/HttpClientMock/issues/3
                implementation "com.pgs-soft:HttpClientMock:1.0.0"
            }

            targets {
                all {
                    testTask.configure {
                        testLogging {
                            exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
                            showStandardStreams = true
                        }
                    }
                }
            }
        }
        integration(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            useJUnitJupiter()

            dependencies {
                implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
                implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
                implementation "org.testcontainers:junit-jupiter:1.19.8"
                implementation "org.testcontainers:openfga:1.19.8"
                implementation project()
            }

            sources {
                java {
                    srcDirs = ['src/test-integration/java']
                }
            }

            targets {
                all {
                    testTask.configure {
                        testLogging {
                            exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
                            showStandardStreams = true
                        }
                    }
                }
            }
        }
    }
}

// Use spotless plugin to automatically format code, remove unused import, etc
// To apply changes directly to the file, run `gradlew spotlessApply`
// Ref: https://github.com/diffplug/spotless/tree/main/plugin-gradle
spotless {
    // comment out below to run spotless as part of the `check` task
    enforceCheck false
    format 'misc', {
        // define the files (e.g. '*.gradle', '*.md') to apply `misc` to
        target '.gitignore'
        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces() // Takes an integer argument if you don't like 4
        endWithNewline()
    }
    java {
        palantirJavaFormat()
        removeUnusedImports()
        importOrder()
    }
}

// Use spotless plugin to automatically format code, remove unused import, etc
// To apply changes directly to the file, run `gradlew spotlessApply`
// Ref: https://github.com/diffplug/spotless/tree/main/plugin-gradle
tasks.register('fmt') {
    dependsOn 'spotlessApply'
}

tasks.register('test-integration') {
    dependsOn testing.suites.integration
}

tasks.named('check').configure {
    dependsOn 'spotlessCheck'
}
